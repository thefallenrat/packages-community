# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Maintainer: Roland Singer <roland@manjaro.org>
# Maintainer: Philip Mueller <philm@manjaro.org>
# Maintainer: FadeMind < fademind@gmail.com>

### NOTE ### Forked sources have merged ALL major fixes from develop branch (https://github.com/Bumblebee-Project/Bumblebee/commits/develop)
### Reference: https://forum.manjaro.org/t/bumblebee-not-switching-back-to-igpu-after-quitting-optirun/1054/12?u=fademind

_commit=0851da6818e265d354d61947021c512f2e51ffce # lastest commit from 20170227
pkgname=bumblebee
pkgver=3.2.1
pkgrel=18
pkgdesc="NVIDIA Optimus support for Linux through Primus/VirtualGL"
arch=('i686' 'x86_64')
depends=('kmod' 'primus' 'glib2' 'mesa-libgl')
makedepends=('help2man')
optdepends=('xf86-video-nouveau: nouveau driver'
            'nouveau-dri: 3D acceleration features fo nouveau'
            'bbswitch: switch on/off discrete card' 
            'nvidia: NVIDIA kernel driver'
            'virtualgl: alternative back-end for optirun')
optdepends_x86_64=('lib32-virtualgl: run 32bit applications with optirun'
                   'lib32-primus: faster back-end for optirun')

url="http://www.bumblebee-project.org"
license=("GPL3")
backup=('etc/bumblebee/bumblebee.conf' 
        'etc/bumblebee/xorg.conf.nouveau' 
        'etc/bumblebee/xorg.conf.nvidia')
install='bumblebee.install'
source=("${pkgname}.zip::https://github.com/FadeMind/Bumblebee/archive/${_commit}.zip")
sha256sums=('e50a3948c3320f65cd449a6dbe1c744cfc671a42ccb529a7dc797d1191a3df1c')

build() {    
    cd "${srcdir}/Bumblebee-${_commit}"
    autoreconf -fi
    ./configure \
        CONF_DRIVER_MODULE_NVIDIA=nvidia \
        CONF_LDPATH_NVIDIA=/usr/lib/nvidia:/usr/lib32/nvidia:/usr/lib:/usr/lib32 \
        CONF_MODPATH_NVIDIA=/usr/lib/nvidia/xorg,/usr/lib/xorg/modules \
        --prefix=/usr \
        --sbindir=/usr/bin \
        --with-udev-rules=/usr/lib/udev/rules.d \
        --sysconfdir=/etc \
        --without-pidfile

    make
}

package() {
    cd "${srcdir}/Bumblebee-${_commit}"
    
    # Install main app
    make install DESTDIR="$pkgdir"
    
    # Install systemd unit
    install -Dm644 "scripts/systemd/bumblebeed.service" "${pkgdir}/usr/lib/systemd/system/bumblebeed.service"    
    sed -i "s/sbin/bin/" "${pkgdir}/usr/lib/systemd/system/bumblebeed.service"
    
    # Install udev rule to prevent GH-#144
    install -Dm644 "conf/99-bumblebee-nvidia-dev.rules" "${pkgdir}/usr/lib/udev/rules.d/99-bumblebee-nvidia-dev.rules"
    
    # Stop nvidia from loading on boot
    mkdir -p "${pkgdir}/etc/modprobe.d"
    echo "remove nvidia modprobe -r --ignore-remove nvidia-drm nvidia-modeset nvidia-uvm nvidia" > "${pkgdir}/etc/modprobe.d/bumblebee.conf"
}
