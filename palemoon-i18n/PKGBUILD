# Maintainer: Stefano Capitani <stefano@manjaro.org>

pkgbase=palemoon-i18n
pkgver=27.5.0
pkgrel=1
pkgdesc="Language pack for Palemoon"
arch=('any')
url="http://www.palemoon.org/langpacks.shtml"
license=('MPL' 'GPL')
depends=("palemoon>=27")

_languages=(
#'ar    "Arabic"'
'cs    "Czech"'
#'da    "Danish"'
'de    "German"'
#'el    "Greek (Modern)"'
'en-GB "English (U.K.)"'
#'eo    "Esperanto"'
'es-AR "Spanish (Argentina)"'
'es-ES "Spanish (Spain)"'
'es-MX "Spanish (Mexico)"'
#'fi    "Finnish"'
'fr    "French"'
#'gl-ES "Galician (Spain)"'
#'hr    "Croatian"'
'hu    "Hungarian"'
#'is    "Icelandic"'
'it    "Italian"'
#'ja    "Japanese"'
#'kn    "Kannada (India)"'
'ko    "Korean"'
'nl    "Dutch"'
'pl    "Polish"'
'pt-BR "Portuguese (Brazil)"'
'pt-PT "Portuguese (Portugal)"'
#'ro    "Romanian/Moldavian"'
'ru    "Russian"'
#'sk    "Slovak"'
#'sl    "Slovenian"'
#'sr    "Serbian"'
'sv-SE "Swedish"'
'tr    "Turkish"'
#'vi    "Vietnamese"'
'zh-CN "Chinese (Simplified)"'
#'zh-TW "Chinese (Traditional)"'
)

pkgname=()
source=()
_url="http://relmirror.palemoon.org/langpacks/${pkgver%.*.*}.x"

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=palemoon-i18n-${_locale,,}

  pkgname+=($_pkgname)
  source+=("palemoon-i18n-$pkgver-$_locale.xpi::$_url/$_locale.xpi")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

# Don't extract anything
noextract=(${source[@]%%::*})

_package() {
  pkgdesc="$2 language pack for Palemoon"
  install -Dm644 palemoon-i18n-$pkgver-$1.xpi \
    "$pkgdir/usr/lib/palemoon/browser/extensions/langpack-$1@palemoon.org.xpi"
}

md5sums=('dbc2666c87b2662704b98c5b3dc476d4'
         '37bcf8b9eb19b5cbc82703c8be3215c9'
         '2b161156619817a0a472f9677877a6c5'
         '1ab80b8f717c1a3535632213ef5d62e4'
         '58ab0c4eec8ac31e6d834c6ab4786da1'
         '6305da70600abf89411fa429fd06cdb1'
         '411f32d3e31851ba193a412b0c5e68ba'
         '0b84e7e95079ec546b6be74a6ae0c41a'
         '51817e453e150b94eec5215b6a0afa4e'
         'a4c2e26746e2e6c91091b8bfa57e890b'
         'b69b0a45ac4f005a151c4e7831e83eac'
         '112dd14955a50d66b86892dcd37236c6'
         '1a03b914df31baf6812f5c9d234c3406'
         'b741038c06026575d0cdf3d7b8fcd73e'
         'd77720e32db93fa1711b9448b92624c5'
         '345b8ab563c5ef26b06213b5275a5ee1'
         '7802238b911e7828e8015c303a66b726'
         '67f12f8f9a32ee4bbc86c9c76e4dc021')
