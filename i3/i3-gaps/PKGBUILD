# Maintainer: Ingo BÃ¼rk <admin at airblader dot de>
# Maintainer: Bernhard Landauer <oberon@manjaro.org>

pkgname=i3-gaps
pkgver=4.14
pkgrel=1
pkgdesc='A fork of i3wm tiling window manager with more features, including gaps'
url=https://github.com/Airblader/i3
arch=('i686' 'x86_64')
license=('BSD')
provides=('i3-wm')
conflicts=('i3-wm' 'i3bar' 'i3bar-git' 'i3-git' 'i3-gaps-git' 'i3-gaps-next-git')
groups=('i3-manjaro')
depends=('xcb-util-keysyms' 'xcb-util-wm' 'libev' 'yajl'
         'startup-notification' 'pango' 'perl' 'xcb-util-cursor' 'xcb-util-xrm'
         'libxkbcommon-x11')
makedepends=('asciidoc' 'docbook-xsl' 'pkgconfig')
optdepends=('dmenu: As menu.'
	'i3lock: For locking your screen.'
	'i3status: To display system information with a bar.'
	'perl-anyevent-i3: For i3-save-tree'
	'perl-json-xs: For i3-save-tree'
	'rxvt-unicode: The terminal emulator used in the default config.')
	
options=('docs' '!strip')
source=("$url/archive/$pkgver.tar.gz")
sha1sums=('7457b0d650373b7bede45e0dcc3a633743af17ae')

build() {
  cd i3-$pkgver

  autoreconf --force --install

  rm -rf build/
  mkdir -p build && cd build/

  ../configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-sanitizers

  # See https://lists.archlinux.org/pipermail/arch-dev-public/2013-April/024776.html
  make CPPFLAGS+="-U_FORTIFY_SOURCE"
}

package() {
  cd i3-$pkgver
  cd build/

  make DESTDIR=$pkgdir/ install

  install -Dm644 ../LICENSE \
    $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
