# Maintainer: FadeMind <fademind@gmail.com>
# Contributor: eloaders <eloaders at linux dot pl>

pkgname=i-nex
_gitname=I-Nex
_gitcommit=3abd55c
pkgver=7.4.0
pkgrel=4
pkgdesc="System information tool like hardinfo, sysinfo"
arch=('i686' 'x86_64')
url="http://i-nex.linux.pl/"
license=('LGPL3')
depends=('gambas3-runtime' 
	 'gambas3-gb-image' 
	 'gambas3-gb-form'
         'gambas3-gb-desktop'
         'gambas3-gb-qt4'
         'gambas3-gb-desktop-x11'
         'python2' 
         'libcpuid'
         'xorg-server-utils' 
         'lsb-release' 
         'curl'
	 'procps-ng')
makedepends=('gambas3-devel' 'gcc' 'imagemagick' 'git')
source=("git+https://github.com/eloaders/${_gitname}.git#commit=${_gitcommit}")
md5sums=('SKIP')
provides=('i-nex-git' 'i-nex-bzr' 'i-nex-dev')
conflicts=('i-nex-git' 'i-nex-bzr' 'i-nex-dev')

prepare() {
    cd ${_gitname}
    sed -i 's|python3$|python2|' pastebinit
    sed -i -e 's|^STATIC.*|STATIC = false|' i-nex.mk
    sed -i -e 's|^UDEV_RULES_DIR.*|UDEV_RULES_DIR = /usr/lib/udev/rules.d|' i-nex.mk
    cd ${_gitname}
    autoreconf -i
}
 
build() {
    cd ${_gitname}/${_gitname}
    ./configure --prefix=/usr
    cd ..
    make
}
 
package() {
    cd ${_gitname}
    make install DESTDIR="$pkgdir"
}
