# Maintainer: Stefano Capitani <stefanoatmanjarodotorg>
# Maintainer: Sam Burgos < sam dot burgos1089 at gmail dot com >

pkgname='lightdm-slick-greeter'
_pkgname='slick-greeter'
pkgver=1.0.8
pkgrel=2
pkgdesc='A slick-looking LightDM greeter'
arch=(i686 x86_64)
url="https://github.com/linuxmint/${_pkgname}"
license=('GPL3')
source=("${url}/archive/${pkgver}.tar.gz"
        "$_pkgname.conf")
depends=('cairo'
    'freetype2'
    'gnome-common'
    'gtk3'
    'libcanberra'
    'libxext'
    'lightdm'
    'manjaro-wallpapers-17.0'
    'pixman')
makedepends=('intltool'
    'vala')
backup=('etc/lightdm/slick-greeter.conf')
sha256sums=('20df7dbb03f65a1e8a74beb0db0b411b2893eb5a7fdbc7514014af01e59dec7d'
            '65049a7fe68d8adce0b1f288be4e769ec07cdbef2d8952ec7827f818aa54d839')

build() {
    cd ${_pkgname}-${pkgver}
    aclocal --install
    autoreconf -vfi
    intltoolize -f
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --sbindir=/usr/bin \
        --libexecdir=/usr/lib/lightdm
    make
}

package() {
	cd ${_pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
# adjust launcher name
    mv $pkgdir/usr/share/xgreeters/slick-greeter.desktop \
      $pkgdir/usr/share/xgreeters/lightdm-slick-greeter.desktop
# Install default conf 
    install -Dm644 $srcdir/$_pkgname.conf $pkgdir/etc/lightdm/$_pkgname.conf
}
