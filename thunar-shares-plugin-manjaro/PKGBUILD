# Maintainer : Rob McCathie <korrode at gmail>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: vinz <vinz@archlinux.us>
# Contributor: Aurélien DESBRIÈRES <ice.cube@gmx.com>
# Contributor: Alexandru Ianu <alexandru.ianu at gmail>

_name=thunar-shares-plugin
pkgname=thunar-shares-plugin-manjaro
pkgver=0.2.0.git
_gitcommit=e24efa98e596fb16622f3e42ebfcc75f06eae420
pkgrel=3
pkgdesc="Thunar plugin to quickly share a folder using Samba without requiring root access (with auto-setup)"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/thunar-plugins/${_name}"
license=('GPL2' 'LGPL')
depends=('thunar' 'samba')
makedepends=('xfce4-dev-tools')
backup=('etc/samba/smb.conf')
provides=("${_name}=${pkgver}")
conflicts=("${_name}")
install="${pkgname}.install"
source=("http://git.xfce.org/thunar-plugins/${_name}/snapshot/${_name}-${_gitcommit}.tar.bz2"
        "smb.conf")
sha1sums=('fbea3c7bc87c766910e41b78d73bfe2f4f48c731'
          'c343d104f0fc91390ac07024c2375b5b459f711b')

build() {
  cd "${srcdir}/${_name}-${_gitcommit}"
  ./autogen.sh --prefix=/usr --enable-debug=no
  make
}

package() {
  cd "${srcdir}/${_name}-${_gitcommit}"
  make DESTDIR="${pkgdir}" install
  
  install -Dm644 "${srcdir}/smb.conf" "${pkgdir}/etc/samba/smb.conf"
  mkdir -p "${pkgdir}/var/lib/samba/usershare"
  chmod 1775 "${pkgdir}/var/lib/samba/usershare"
}
