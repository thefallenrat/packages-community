pkgname=locale-info
pkgver=2.21
pkgrel=3
pkgdesc="Locale informations"
arch=('any')
url="http://www.gnu.org/software/libc"
license=('GPL' 'LGPL')

source=(http://ftp.gnu.org/gnu/libc/glibc-${pkgver}.tar.xz
        localeinfo.sh)

sha256sums=('aeeb362437965a5d3f40b151094ca79def04a115bd363fdd4a9a0c69482923b8'
            '3c96d41a85f3b83debe4133cc922b08904960e15bff38ad9600ef4093d0c4646')

build() {
  cd ${srcdir}

  echo "building LOCALESINFO file..."
  ./localeinfo.sh "glibc-${pkgver}/localedata/locales" LOCALESINFO
}


package() {
  cd ${srcdir}/glibc-${pkgver}

  install -dm755 ${pkgdir}/usr/share/i18n
  install -m644 localedata/SUPPORTED ${pkgdir}/usr/share/i18n/
  install -m644 ${srcdir}/LOCALESINFO ${pkgdir}/usr/share/i18n/
}

