# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dot]org>
# Contributor: artoo <artoo@manjaro.org>

pkgbase=desktop-settings
pkgname=('manjaro-bspwm-settings'
        'manjaro-budgie-settings'
        'manjaro-jwm-settings'
        'manjaro-lxde-settings'
        'manjaro-mate-settings'
        'manjaro-netbook-settings'
        'manjaro-pantheon-settings')
pkgver=20170501
pkgrel=1
arch=('any')
url="https://github.com/manjaro/desktop-settings"
license=('GPL')
makedepends=('git')
source=("git+$url.git")
sha256sums=('SKIP')

_inst_dir(){
    if [[ -d $srcdir/$pkgbase/$1/skel ]]; then
        install -d $pkgdir/etc
        cp -r $srcdir/$pkgbase/$1/skel $pkgdir/etc
    fi

    if [[ -d $srcdir/$pkgbase/$1/scripts ]]; then
        install -d $pkgdir/usr/bin
        cp $srcdir/$pkgbase/$1/scripts/* $pkgdir/usr/bin
    fi

    if [[ -d $srcdir/$pkgbase/$1/schemas ]]; then
        install -d $pkgdir/usr/share/glib-2.0/schemas
        cp $srcdir/$pkgbase/$1/schemas/* $pkgdir/usr/share/glib-2.0/schemas
    fi
}

pkgver() {
    date +%Y%m%d
}

package_manjaro-bspwm-settings() {
    pkgdesc='Manjaro Linux bspwm settings'
    conflicts=('manjaro-desktop-settings')
    provides=('manjaro-desktop-settings')
    depends=('bspwm-wallpapers'
        'xcursor-breeze'
        'feh'
        'gcc' # needed for .extend.Xresources
        'rootmenu'
        'mygtkmenui'
        'limepanel'
        'bspwm-manjaro'
        'bspwm-scripts'
        'sxhkd'
        'manjaro-base-skel')
    _inst_dir 'community/bspwm'
}

package_manjaro-budgie-settings() {
    pkgdesc='Manjaro Linux budgie settings'
    depends=('budgie-desktop'
        'arc-themes-maia'
        'vertex-maia-icon-theme'
        'budgie-wallpapers'
        'xcursor-breeze'
	'qt5-styleplugins'
        'qt5ct'
        'manjaro-base-skel')
    optdepends=('plymouth-theme-manjaro-fancy-budgie' 'firefox-arc-maia-settings')
    conflicts=('manjaro-desktop-settings')
    provides=('manjaro-desktop-settings')

    _inst_dir 'community/budgie'
}

package_manjaro-jwm-settings() {
    pkgdesc='Manjaro Linux jwm settings'
    depends=('cantarell-fonts'
        'gcc' # needed for .extend.Xresources
        'gtk-theme-stylishdark'
        'jwm-artwork'
        'jwm-wallpapers'
        'ultraflatorange-icon-theme'
        'xcursor-rezowhite'
        'manjaro-base-skel')
    conflicts=('manjaro-desktop-settings')
    provides=('manjaro-desktop-settings')

    _inst_dir 'community/jwm'
}

package_manjaro-lxde-settings() {
    pkgdesc='Manjaro Linux lxde settings'
    depends=('manjaro-base-skel'
    	     'arc-maia-icon-theme'
	     'vertex-maia-themes'
	     'lxde-wallpapers'
	     'qt5ct'
	     'qt5-styleplugins'
	     'manjaro-lxde-logout-banner'
	     'manjaro-lxde-xfce4-notifyd'
	     'i3-scrot')
    conflicts=('manjaro-desktop-settings')
    provides=('manjaro-desktop-settings')

    _inst_dir 'community/lxde'
}

package_manjaro-mate-settings() {
    pkgdesc='Manjaro Linux mate settings'
    depends=('manjaro-base-skel'
		'mate-desktop'
		'mate-menus'
		'caja'
		'marco'
		'mate-control-center'
		'mate-polkit'
		'mate-session-manager'
		'mate-settings-daemon'
		'brisk-menu'
		'manjaro-mate-panel-layout'
		'mate-wallpapers'
		'mate-notification-theme-slate'
                'xcursor-breeze'
                'arc-themes-maia'
		'arc-maia-icon-theme'
                'maia-wallpaper'
		'gnome-icon-theme'
		'gnome-icon-theme-extras'
		'gnome-icon-theme-symbolic'
                'qt5-styleplugins'
                'qt5ct')
    optdepends=('firefox-arc-maia-settings' 'moka-icon-theme'
'mate-icon-theme' 'mate-themes' 'mate-user-guide')
    conflicts=('manjaro-desktop-settings')
    provides=('manjaro-desktop-settings')

    _inst_dir 'community/mate'
}

package_manjaro-netbook-settings() {
    pkgdesc='Manjaro Linux netbook settings'
    depends=('manjaro-base-skel')
    conflicts=('manjaro-desktop-settings')
    provides=('manjaro-desktop-settings')

    _inst_dir 'community/netbook'
}

package_manjaro-pantheon-settings() {
    pkgdesc='Manjaro Linux pantheon settings'
    depends=('manjaro-base-skel')
    conflicts=('manjaro-desktop-settings')
    provides=('manjaro-desktop-settings')

    _inst_dir 'community/pantheon'
}
