# Maintainer: Stefano Capitani <stefano@manjarodotorg>

_pkgbase=timeshift
pkgname=$_pkgbase-dev
pkgver=v17.2.r41.gdf5b91c
pkgrel=1
pkgdesc="A system restore utility for Linux"
arch=('i686' 'x86_64')
url="https://github.com/teejee2008/$_pkgbase"
license=('GPL')
depends=('gtk3' 'libsoup' 'desktop-file-utils' 'cron' 'rsync')
optdepends=('zensu'
			'gksu')
makedepends=('vala' 'diffutils' 'coreutils' 'vte3' 'libgee' 'git')
conflicts=("$_pkgbase")

source=("git+$url.git#branch=master")
sha256sums=('SKIP')

pkgver() {
    cd $_pkgbase
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
#workaround for wayland session otherwise no GUI 
  cd $_pkgbase/src
  sed -i '4d' timeshift-gtk.desktop
  echo "Exec=sh -c 'xhost +SI:localuser:root && timeshift-launcher'" >> timeshift-gtk.desktop
}

build() {
  cd $_pkgbase/src
  make
}

package() {
  cd $_pkgbase/src
  make DESTDIR=$pkgdir install
}

