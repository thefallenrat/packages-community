# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# AUR-Maintainer: Nate Simon <aurpkg (at natesimon.net)>

pkgname=xreader
pkgver=1.4.4
pkgrel=2
_commit=dfbbd774021b2c0c0161b47d8237997c81ded30b
pkgdesc="Document viewer for files like PDF and Postscript. X-Apps Project"
arch=('i686' 'x86_64')
license=('GPL')
url="https://github.com/linuxmint/${pkgname}"
groups=('x-apps')
depends=('desktop-file-utils'
         'djvulibre'
         'ghostscript'
         'gsettings-desktop-schemas'
         'gtk2'
         'gtk3'
         'gtk3-print-backends'
         'libsecret'
         'libspectre'
         'poppler-glib'
         'webkit2gtk')
makedepends=('git' 'gobject-introspection' 'mate-common' 'nemo' 'yelp-tools')
optdepends=('nemo: nemo extension (must be enabled at compile time)'
    'caja-extensions-common: caja extension (must be enabled at compile time)'
    'texlive-bin: support for dvi files')
source=("git+$url.git#commit=$_commit")
#source=("${url}/archive/${pkgver}.tar.gz")
md5sums=('SKIP')

build() {
    cd $pkgname
    ./autogen.sh --prefix="/usr" \
        --localstatedir="/var" \
        --libexecdir="/usr/lib/${pkgname}" \
        --disable-caja \
        --enable-introspection \
        --enable-epub
    make
}

package(){
    cd $pkgname
    make DESTDIR="$pkgdir/" install
}
