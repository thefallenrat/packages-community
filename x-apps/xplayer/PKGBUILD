# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# AUR-Maintainer: Nate Simon <aurpkg (at natesimon.net)>

pkgname=xplayer
pkgver=1.4.2
pkgrel=2
pkgdesc="Simple media player. X-Apps Project"
arch=('i686' 'x86_64')
license=('GPL')
groups=('x-apps')
depends=('clutter-gst'
    'clutter-gtk'
    'dbus-glib'
    'desktop-file-utils'
    'gsettings-desktop-schemas'
    'gst-plugins-base'
    'gst-plugins-good'
    'iso-codes'
    'libpeas'
    'libxkbfile'
    'openssl-1.0'
    'python2-gobject'
    'xapps'
    'xplayer-plparser'
    'yelp-xsl')
makedepends=('git'
    'gnome-common'
    'gobject-introspection'
    'gtk-doc'
    'vala')
optdepends=('gst-libav: Extra media codec support')
url="https://github.com/linuxmint/${pkgname}"
source=("${url}/archive/${pkgver}.tar.gz")
md5sums=('193d80c1bdb9323275bfe1e8134175b5')

build() {
    cd ${srcdir}/${pkgname}-${pkgver}

    # https://github.com/linuxmint/xplayer/issues/22
    # No switch to disable grilo, so just bump the required
    # version to something that obviously won't be satisfied.
    sed -i 's/GRILO_REQS=0.2.0/GRILO_REQS=0.9.0/g' configure.ac

    ./autogen.sh --prefix="/usr" \
        --localstatedir="/var" \
        --libexecdir="/usr/lib/${pkgname}" \
        --enable-browser-plugins=no
    make
}

package(){
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR="$pkgdir/" install
}
