# Maintainer: Carl George < arch at cgtx dot us >
# Contributor: XZS < d dot f dot fischer at web dot de >
# Contributor: Janne Haapsaari < haaja at iki dot fi >
# Contributor: Christopher Kroo√ü < didi2002 at web dot de >
# Maintainer: Stefano Capitani <stefano@manjaro.org>

_extname="dash-to-dock"
_author="micheleg"

pkgname="gnome-shell-extension-${_extname}"
pkgver=60
pkgrel=1
pkgdesc="This extension moves the dash out of the overview transforming it in a dock for an easier launching of applications and a faster switching between windows and desktops."
arch=("any")
url="https://${_author}.github.io/${_extname}/"
license=("GPL2")
depends=('dconf' 'gnome-shell') 
makedepends=("gnome-common" "intltool")
conflicts=("gnome-shell-extensions-dash-to-dock-git")
source=("https://github.com/${_author}/${_extname}/archive/extensions.gnome.org-v${pkgver}.tar.gz")
noextract=()
sha256sums=('9a7f526edbaacf2cc032ead805fb11184694927e1b50bd74c573938fdf21212e')

build() {
    cd "${srcdir}/${_extname}-extensions.gnome.org-v${pkgver}"
    make
}

package() {
    cd "${srcdir}/${_extname}-extensions.gnome.org-v${pkgver}"
    make \
        INSTALLBASE="${pkgdir}/usr/share/gnome-shell/extensions" \
        VERSION="${pkgver}" \
        install
    _gschema="schemas/org.gnome.shell.extensions.dash-to-dock.gschema.xml"
    install -Dm0644 "${_gschema}" "${pkgdir}/usr/share/glib-2.0/${_gschema}"
}
