# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-session-ui
_pkgname=$pkgname-manjaro
pkgver=4.0.13.1
pkgrel=2
pkgdesc='Deepin desktop-environment - Session UI module'
arch=('i686' 'x86_64')
url='https://github.com/oberon2007/deepin-session-ui-manjaro'
_commit=935f020f9b9f5b69c1c322863732e2897ab183d4
license=('GPL3')
groups=('deepin')
depends=('deepin-control-center'
    'deepin-daemon'
    'deepin-qt-dbus-factory'
    'deepin-tool-kit'
    'gsettings-qt'
    'gtk2'
    'liblightdm-qt5'
    'qt5-svg'
    'startdde'
    'xorg-xrandr')
makedepends=('git' 'qt5-tools' 'deepin-gettext-tools')
provides=('lightdm-deepin-greeter')
conflicts=('dde-workspace' 'dde-session-ui')
replaces=('dde-workspace' 'dde-session-ui')
source=("git+$url.git#commit=$_commit"
    compatible_dtk.patch::https://github.com/linuxdeepin/dde-session-ui/commit/e078009ae5152b28d7f051ac1340561c7dba04d6.patch)
sha512sums=('SKIP'
            'fd0a415c450dbd42124edd99e421b351e9f83f2e6ff292e46b406b1bc0df78bc3493572e879c567aad654b0ab22efce8443e17572af406016eee803044cce7f4')

prepare() {
    cd $_pkgname
    patch -p1 -i $srcdir/compatible_dtk.patch
}

build() {
    cd $_pkgname
    qmake-qt5 PREFIX=/usr
    make
}

package() {
    cd $_pkgname
    make INSTALL_ROOT="$pkgdir" install
}
