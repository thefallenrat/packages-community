# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=budgie-desktop
_helper_pkgname=budgie-helper
pkgver=10.4
_helper_gitrev=3b52434b6d1cfe00c5bac1046d323813ce531a55
pkgrel=0
pkgdesc="Modern desktop environment from the Solus Project"
arch=('i686' 'x86_64')
url="https://solus-project.com/budgie"
license=('GPL' 'LGPL')
depends=('accountsservice' 'gnome-bluetooth' 'gnome-menus' 'gnome-session' 'gnome-themes-standard' 'libibus' 'libpeas' 'libwnck3' 'mutter')
makedepends=('autoconf-archive' 'git' 'gobject-introspection' 'intltool' 'meson' 'vala' 'sassc')
optdepends=('gnome-backgrounds: Default background'
            'gnome-control-center: System settings'
            'gnome-screensaver: Lock screen'
            'network-manager-applet: Network management')
source=("https://github.com/solus-project/budgie-desktop/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz"{,.asc}
        "$_helper_pkgname-$_helper_gitrev.tar.gz::https://codeload.github.com/City-busz/$_helper_pkgname/tar.gz/$_helper_gitrev")
validpgpkeys=('8876CC8EDAEC52CEAB7742E778E2387015C1205F') # Ikey Doherty (Solus Project Founder)
sha256sums=('17da8efe56a73426117f89eed8f2a1236c592042acb8d3fec063b24ebe124245'
            'SKIP'
            '3a4d7dd7c95ccba4e2916adf4a14769ffe54e8f86ed302d0268cd312b2a85c0e')

prepare() {
	mkdir build
	cd $pkgname-$pkgver

	# Provide better compatibility for GNOME
	# https://github.com/solus-project/budgie-desktop/issues/261
	cd "$srcdir/$_helper_pkgname-$_helper_gitrev"
	NOCONFIGURE=1 ./autogen.sh
}

build() {
	cd build
	meson --prefix=/usr --buildtype=release ../$pkgname-$pkgver \
		--sysconfdir=/etc \
		-Dwith-gtk-doc=false
	ninja

	cd "$srcdir/$_helper_pkgname-$_helper_gitrev"
	./configure --prefix=/usr --sysconfdir=/etc --disable-schemas-compile --disable-Werror
	make
}

package() {
	cd build
	DESTDIR="$pkgdir" ninja install

	cd "$srcdir/$_helper_pkgname-$_helper_gitrev"
	make DESTDIR="$pkgdir" install
}
