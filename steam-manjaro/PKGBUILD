# $Id$
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: K900 <k0009000@gmail.com>
# Manjaro implementation: Alexandru Ianu <alexandru@manjaro.org>
pkgname=steam-manjaro
_pkgname=steam
pkgver=1.0.0.50
pkgrel=0.1
pkgdesc="Digital distribution client bootstrap package. Implementation for bleeding edge Arch-based rolling systems."
arch=("i686" "x86_64")
url="http://steampowered.com/"
license=('custom')
install=steam.install
source=("http://repo.steampowered.com/$_pkgname/pool/$_pkgname/s/$_pkgname/${_pkgname}_$pkgver.tar.gz"
        'lib32-flashplugin-path.patch'
        'alsa_sdl_audiodriver.patch'
        'lib_path_and_close_min.patch'
        "steam.desktop"
        "steam-valve.desktop"
        "steam-clean.desktop"
        "steam-clean"
        "51-steam.rules")
sha256sums=('be6388c0ab36799d6173276ac75b4d92c6623a53d901395c9fc1963cfd245573'
         '87b9e9f74709a43fb8424d7bb3074fb576f72c56d78a045e3b4ed4719600b0fa'
         '174a110eda1c9d5b1c92a4490b266b31918559bbf8292a94905221c92da4bc0e'
         'ad0dac17e97be528ed414e8aae1fe645c4f92e11769982801602f22de9425c71'
         '4cd23352ffd402830e47e824282b6dbdd51b2ae4c902d71e4f1909bf5a080477'
         '9c4c59ad68895ca9ec5d174ec68f2e39d8ac9ec31e5f5e4a1b25100e8c40bbc1'
         '3273349686e2d80420f060e089456884913fd6ed469eb7f03805b6aa2a271be9'
         '8bbcbd2f29ef1ac62b49d9c2770be9140a447301dcdeb83c94d1f3800228b360'
         '12d6d0a96e66f10eaeee94ddb6d1048c0e40a8132521fa7b00b844d6454d641c')

if [[ "$CARCH" == x86_64 ]]; then
    source+=("https://www.dropbox.com/s/9xxku2o253ft6r0/lib32-nm09810.tar.xz")
    sha256sums+=('b56b9da85e5647ef2b8013d14378b30917d7701cd61b27ba6a39045ab5ecc529')
fi

provides=('steam' 'steam-native')
replaces=('steam-native')

prepare() {
    if [[ "$CARCH" == "x86_64" ]]; then
        #depends+=(lib32-libgl lib32-mesa-libgl lib32-gcc-libs lib32-libx11)
        optdepends=(
            'lib32-mesa-dri: for open source driver users'
            'lib32-catalyst-utils: for AMD Catalyst users'
            'lib32-nvidia-utils: for NVIDIA proprietary blob users'
            'lib32-flashplugin: for flash video'
            'lib32-alsa-plugins: for pulseaudio on some games'
        )
        patch -d "$_pkgname" -Np1 -i "$srcdir/lib32-flashplugin-path.patch"
    else
        optdepends=('flashplugin: for flash videos')
    fi
    patch -d "$_pkgname" -Np1 -i "$srcdir/alsa_sdl_audiodriver.patch"
    patch -d "$_pkgname" -Np1 -i "$srcdir/lib_path_and_close_min.patch"
}

package() {
    depends=(
      'bash' 'desktop-file-utils' 'hicolor-icon-theme' 'curl'
      'dbus' 'freetype2'  'gdk-pixbuf2' 'ttf-font' 'zenity'
      'libindicator-gtk2' 'libindicator-gtk3' 'dhcp-client'
      'networkmanager' 'wpa_supplicant' 'mono' 'mono-addins'
      )

    if [[ "$CARCH" == x86_64 ]]; then
        depends+=('lib32-libgl' 'lib32-gcc-libs' 'lib32-libx11')
        #Steam native deps
        depends+=('lib32-libxinerama' 'lib32-pango' 'lib32-openal' 'lib32-gdk-pixbuf2'
                  'lib32-gtk2' 'lib32-libgcrypt15' 'lib32-dbus-glib' 'lib32-libusb'
                  'lib32-libnl' 'lib32-polkit' 'lib32-pam' 'lib32-cracklib' 'lib32-libtirpc'
                  'lib32-js17' 'lib32-libpng12' 'lib32-systemd' 'lib32-gconf'
                  'lib32-libappindicator' 'lib32-nss' 'lib32-libsoup' 'lib32-polkit'
                  'lib32-libnl' 'lib32-dbus-glib' 'iproute2' 'lib32-libpulse')
        #Common game deps
        depends+=('lib32-sdl' 'lib32-sdl_image' 'lib32-sdl_mixer' 'lib32-sdl2' 'lib32-sdl2_image'
                  'lib32-tcp_wrappers' 'lib32-speex' 'lib32-gperftools' 'lib32-libcurl-gnutls')
        #Provides/conflicts
        provides+=('lib32-libudev.so.0' 'lib32-networkmanager'=0.9.8.10 'lib32-nm-glib'=0.9.8.10)
        conflicts+=('chromium-browser-bin' 'lib32-libudev.so.0' 'lib32-networkmanager' 'lib32-nm-glib')
        #Compat udev/NM
        mkdir -p "$pkgdir/usr/lib32"
        ln -s /usr/lib32/libudev.so ${pkgdir}/usr/lib32/libudev.so.0
        tar -xJf "$srcdir/lib32-nm09810.tar.xz" -C "$pkgdir" --no-same-owner
    else
        #Steam native deps
        depends+=('libxinerama' 'pango' 'openal' 'gtk2' 'dbus-glib' 'libusb' 'libnl'
                  'polkit' 'pam' 'cracklib' 'libtirpc' 'js17' 'libpng12' 'libgcrypt15'
                  'systemd' 'gconf' 'libappindicator-gtk2' 'libappindicator-gtk3'
                  'nss' 'libpulse')
        #Common game deps
        depends+=('sdl' 'sdl_image' 'sdl_mixer' 'sdl2' 'sdl2_image' 'tcp_wrappers' 'speex' 'gperftools' 'libcurl-gnutls')
        #Provides/conflicts
        provides+=('libudev.so.0')
        conflicts+=('chromium-browser-bin' 'libudev.so.0')
        #Compat udev
        mkdir -p "$pkgdir/usr/lib"
        ln -s /usr/lib/libudev.so ${pkgdir}/usr/lib/libudev.so.0
    fi

    mkdir -p "$pkgdir/usr/share/applications"
    cp "$srcdir/steam.desktop" "$pkgdir/usr/share/applications/steam.desktop"
    chmod +x "$pkgdir/usr/share/applications/steam.desktop"
    cp "$srcdir/steam-valve.desktop" "$pkgdir/usr/share/applications/steam-valve.desktop"
    chmod +x "$pkgdir/usr/share/applications/steam-valve.desktop"
    cp "$srcdir/steam-clean.desktop" "$pkgdir/usr/share/applications/steam-clean.desktop"
    chmod +x "$pkgdir/usr/share/applications/steam-clean.desktop"
    mkdir -p "$pkgdir/usr/bin"
    cp "$srcdir/steam-clean" "$pkgdir/usr/bin/steam-clean"
    chmod +x "$pkgdir/usr/bin/steam-clean"

    # Install license
    make -C "$_pkgname" DESTDIR="$pkgdir" install
    install -Dm644 "$pkgdir/usr/share/doc/steam/steam_install_agreement.txt" "$pkgdir/usr/share/licenses/steam/LICENSE"

    # blank steamdeps because apt-get
    ln -sf /bin/true "$pkgdir/usr/bin/steamdeps"

    # Polkit rule
    mkdir -p "$pkgdir/etc/polkit-1/rules.d"
    cp "$srcdir/51-steam.rules" "$pkgdir/etc/polkit-1/rules.d/51-manjaro.rules"
    chmod 700 "$pkgdir/etc/polkit-1/rules.d"
    chmod 700 "$pkgdir/etc/polkit-1/rules.d/51-manjaro.rules"

    # Replace orig launcher
    rm "$pkgdir/usr/share/applications/steam.desktop" 
    cp "$srcdir/steam.desktop" "$pkgdir/usr/share/applications/steam.desktop"
    chmod +x "$pkgdir/usr/share/applications/steam.desktop"
}
