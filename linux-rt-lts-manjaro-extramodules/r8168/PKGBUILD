# Based on the file created for Arch Linux by:
# Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Bob Fanger < bfanger(at)gmail >
# Filip <fila pruda com>, Det < nimetonmaili(at)gmail >

# Maintainer: Philip MÃ¼ller <philm@manjaro.org>
# Maintainer: Bernhard Landauer <oberon@manjaro.org>

pkgname=linux-rt-lts-manjaro-r8168
_pkgname=r8168
pkgver=8.043.02
pkgrel=2
pkgdesc="A kernel module for Realtek 8168 network cards"
url="http://www.realtek.com.tw"
license=("GPL")
arch=('x86_64')
depends=('glibc' 'linux-rt-lts-manjaro')
makedepends=('linux-rt-lts-manjaro-headers')
provides=("$_pkgname=$pkgver")
groups=('linux-rt-lts-manjaro-extramodules')
source=("https://github.com/mtorromeo/$_pkgname/archive/$pkgver/$pkgver.tar.gz")
sha256sums=('ea22aa1dfda20615f5e114f982f7e61385f871f682eea0c154245760a439abc4')
install=$_pkgname.install

build() {
	_extramodules=extramodules-4.4-rt-lts-MANJARO
	_kernver="$(cat /usr/lib/modules/$_extramodules/version || true)"
	cd "$_pkgname-$pkgver"

	# avoid using the Makefile directly -- it doesn't understand
	# any kernel but the current.
	make -C /usr/lib/modules/$_kernver/build \
	  SUBDIRS="$srcdir/$_pkgname-$pkgver/src" \
	  EXTRA_CFLAGS="-DCONFIG_R8168_NAPI -DCONFIG_R8168_VLAN" \
	  modules
}

package() {
	_extramodules=extramodules-4.4-rt-lts-MANJARO
	cd "$srcdir/$_pkgname-$pkgver/src"
	install -D -m644 $_pkgname.ko "$pkgdir/usr/lib/modules/$_extramodules/$_pkgname.ko"

	# set the kernel we've built for inside the install script
	sed -i -e "s/EXTRAMODULES=.*/EXTRAMODULES=${_extramodules}/g" "${startdir}/${_pkgname}.install"

	find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
}
