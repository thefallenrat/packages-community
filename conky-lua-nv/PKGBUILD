# Maintainer: Spyros Stathopoulos <foucault.online[at]gmail[dot]com>
# Contributor: Jon Wilson <bugs@pandorica.net>
# Contributor: Andrwe Lord Weber <lord-weber-andrwe [at] andrwe [dot] org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: James Rayner <james@archlinux.org>
# Contributor: Partha Chowdhury <kira.laucas@gmail.com>

pkgname=conky-lua-nv
_pkgname=conky
pkgver=1.10.6
pkgrel=1
pkgdesc="An advanced system monitor for X based on torsmo with lua and nvidia enabled"
arch=('i686' 'x86_64')
url="https://github.com/brndnmtthws/$_pkgname"
license=('GPL3' 'BSD')
conflicts=('conky' 'conky-lua')
provides=("conky=$pkgver" 'conky-lua')
depends=('alsa-lib' 'libxml2' 'curl' 'cairo' 'wireless_tools'
	'libxft' 'glib2' 'libxdamage' 'libxinerama' 'imlib2' 'lua51' 'librsvg' 'tolua++')
makedepends=('cmake' 'docbook2x' 'docbook-xml' 'docbook-xsl' 'libxnvctrl'
	'man-db' 'perl-xml-libxml')
optdepends=('nvidia: for GT4xx and newer GPUs',
	'nvidia-340xx: for G8x, G9x, GT2xx GPUS',
	'nvidia-304xx: for GeForce 6/7 GPUs')
source=(https://github.com/brndnmtthws/$_pkgname/archive/v$pkgver.tar.gz
        ascii.patch
        fix_build.patch)
sha1sums=('54cb3322dc3a969f1fda03383012c61d57261345'
          '96cdbc38e8706c8a3120601983df5c7265716128'
          '2ca9d68c6421f9f602bd85cc7b920faa68ae8fec')
options=('!strip' 'debug')

prepare() {
  cd "$srcdir/$_pkgname-$pkgver"
  patch -p1 -i ../ascii.patch
  patch -p1 -i ../fix_build.patch

  cd cmake
}

build() {
  cd $srcdir/$_pkgname-$pkgver

  cmake \
    -D CMAKE_BUILD_TYPE=Release \
    -D MAINTAINER_MODE=ON \
    -D BUILD_CURL=ON \
    -D BUILD_LUA_RSVG=ON \
    -D BUILD_LUA_CAIRO=ON \
    -D BUILD_LUA_IMLIB2=ON \
    -D BUILD_IMLIB2=ON \
    -D BUILD_RSS=ON \
    -D BUILD_WEATHER_METAR=ON \
    -D BUILD_WEATHER_XOAP=ON \
    -D BUILD_WLAN=ON \
    -D BUILD_NVIDIA=ON \
    -D BUILD_XDBE=ON \
    -D CMAKE_INSTALL_PREFIX=/usr \
    .

  make
}

package() {
  cd $srcdir/$_pkgname-$pkgver
  make DESTDIR=$pkgdir install
  install -D -m644 COPYING $pkgdir/usr/share/licenses/$pkgname/LICENSE
  install -D -m644 LICENSE.BSD $pkgdir/usr/share/licenses/$pkgname/LICENSE.BSD
  install -D -m644 extras/vim/syntax/conkyrc.vim $pkgdir/usr/share/vim/vimfiles/syntax/conkyrc.vim
  install -D -m644 extras/vim/ftdetect/conkyrc.vim $pkgdir/usr/share/vim/vimfiles/ftdetect/conkyrc.vim
}
